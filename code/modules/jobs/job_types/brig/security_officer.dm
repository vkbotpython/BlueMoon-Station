/datum/job/officer
	title = "Security Officer"
	flag = OFFICER
	auto_deadmin_role_flags = DEADMIN_POSITION_SECURITY
	department_head = list("Head of Security")
	department_flag = ENGSEC
	faction = "Station"
	total_positions = 5 //Handled in /datum/controller/occupations/proc/setup_officer_positions()
	spawn_positions = 5 //Handled in /datum/controller/occupations/proc/setup_officer_positions()
	supervisors = "the head of security, and the head of your assigned department (if applicable)"
	selection_color = "#c02f2f"
	minimal_player_age = 7
	exp_requirements = 3000
	exp_type = EXP_TYPE_CREW
	considered_combat_role = TRUE
	alt_titles = list(
		"Gorlex Marauders Trainee", //Триглав выше, для удобства
		"Combatant", //Синди выше, для удобства
		"Training Officer", //Инструктор выше, для удобства
		"Security Training Officer", //Инструктор выше, для удобства
		"Field Training Officer", //Инструктор выше, для удобства
		"AC Specialist",
		"Cerberus",
		"Civil Protection",
		"Defense Contractor",
		"Deputy Sheriff",
		"Explosives Specialist",
		"Guard",
		"Guardsman",
		"Police Officer",
		"Probation Officer",
		"Parole Officer",
		"Riot Control Officer",
		"SAARE Operator",
		"Safeguard Agent",
		"Security Agent",
		"Security Guard",
		"Security Technician",
		"K-9 Handler",
		"Service Pet Handler",
		"Slutcurity Officer",
		"Studcurity Officer",
		"Tyranny Lover"
		)

	outfit = /datum/outfit/job/security
	plasma_outfit = /datum/outfit/plasmaman/security

	access = list(ACCESS_SECURITY, ACCESS_SEC_DOORS, ACCESS_BRIG, ACCESS_COURT, ACCESS_MAINT_TUNNELS, ACCESS_MORGUE, ACCESS_WEAPONS, ACCESS_ENTER_GENPOP, ACCESS_LEAVE_GENPOP, ACCESS_FORENSICS_LOCKERS, ACCESS_MINERAL_STOREROOM, ACCESS_PRODUCTION_SECURITY)
	minimal_access = list(ACCESS_SECURITY, ACCESS_SEC_DOORS, ACCESS_BRIG, ACCESS_COURT, ACCESS_WEAPONS, ACCESS_ENTER_GENPOP, ACCESS_LEAVE_GENPOP, ACCESS_MINERAL_STOREROOM, ACCESS_PRODUCTION_SECURITY) // See /datum/job/officer/get_access()
	paycheck = PAYCHECK_HARD
	paycheck_department = ACCOUNT_SEC
	bounty_types = CIV_JOB_SEC
	departments = DEPARTMENT_BITFLAG_SECURITY

	mind_traits = list(TRAIT_LAW_ENFORCEMENT_METABOLISM)

	display_order = JOB_DISPLAY_ORDER_SECURITY_OFFICER
	blacklisted_quirks = list(/datum/quirk/mute, /datum/quirk/brainproblems, /datum/quirk/nonviolent, /datum/quirk/blindness, /datum/quirk/monophobia, /datum/quirk/onelife)
	threat = 2

	family_heirlooms = list(
		/obj/item/book/manual/wiki/security_space_law,
		/obj/item/clothing/head/beret/sec
	)

	mail_goodies = list(
		/obj/item/reagent_containers/food/snacks/donut/caramel = 10,
		/obj/item/reagent_containers/food/snacks/donut/matcha = 10,
		/obj/item/reagent_containers/food/snacks/donut/blumpkin = 5,
//		/obj/item/clothing/mask/whistle = 5,
		/obj/item/melee/baton/boomerang/loaded = 1
	)

/datum/job/officer/get_access()
	var/list/L = list()
	L |= ..() | check_config_for_sec_maint()
	return L

GLOBAL_LIST_INIT(available_depts, list(SEC_DEPT_ENGINEERING, SEC_DEPT_MEDICAL, SEC_DEPT_SCIENCE, SEC_DEPT_SUPPLY))

/datum/job/officer/after_spawn(mob/living/spawned, client/player_client, latejoin = FALSE)
	. = ..()
	if(!ishuman(spawned))
		return
	var/mob/living/carbon/human/H = spawned
	// Assign department security
	var/department
	if(player_client?.prefs)
		department = player_client.prefs.prefered_security_department
		if(!LAZYLEN(GLOB.available_depts) || department == "None")
			return
		else if(department in GLOB.available_depts)
			LAZYREMOVE(GLOB.available_depts, department)
		else
			department = pick_n_take(GLOB.available_depts)
	var/ears = null
	var/accessory = null
	var/list/dep_access = null
	var/destination = null
	var/spawn_point = null
	switch(department)
		if(SEC_DEPT_SUPPLY)
			ears = /obj/item/radio/headset/headset_sec/alt/department/supply
			dep_access = list(ACCESS_MAILSORTING, ACCESS_MINING, ACCESS_MINING_STATION, ACCESS_CARGO)
			destination = /area/security/checkpoint/supply
			spawn_point = locate(/obj/effect/landmark/start/depsec/supply) in GLOB.department_security_spawns
			accessory = /obj/item/clothing/accessory/armband/cargo
		if(SEC_DEPT_ENGINEERING)
			ears = /obj/item/radio/headset/headset_sec/alt/department/engi
			dep_access = list(ACCESS_CONSTRUCTION, ACCESS_ENGINE, ACCESS_ATMOSPHERICS)
			destination = /area/security/checkpoint/engineering
			spawn_point = locate(/obj/effect/landmark/start/depsec/engineering) in GLOB.department_security_spawns
			accessory = /obj/item/clothing/accessory/armband/engine
		if(SEC_DEPT_MEDICAL)
			ears = /obj/item/radio/headset/headset_sec/alt/department/med
			dep_access = list(ACCESS_MEDICAL, ACCESS_MORGUE, ACCESS_SURGERY, ACCESS_CLONING)
			destination = /area/security/checkpoint/medical
			spawn_point = locate(/obj/effect/landmark/start/depsec/medical) in GLOB.department_security_spawns
			accessory =  /obj/item/clothing/accessory/armband/medblue
		if(SEC_DEPT_SCIENCE)
			ears = /obj/item/radio/headset/headset_sec/alt/department/sci
			dep_access = list(ACCESS_RESEARCH, ACCESS_TOX)
			destination = /area/security/checkpoint/science
			spawn_point = locate(/obj/effect/landmark/start/depsec/science) in GLOB.department_security_spawns
			accessory = /obj/item/clothing/accessory/armband/science

	if(accessory)
		var/obj/item/clothing/under/U = H.w_uniform
		U.attach_accessory(new accessory)
	if(ears)
		if(H.ears)
			qdel(H.ears)
		H.equip_to_slot_or_del(new ears(H),ITEM_SLOT_EARS_LEFT) // Sandstorm edit

	// В слоте ID может лежать кошелёк или КПК - у них нет var/access, зато GetID() отдаёт
	// вложенную карту. Мягкий каст на wear_id ловил рантайм и оставлял офицера без доступов.
	var/obj/item/card/id/worn_id = H.wear_id?.GetID()
	if(dep_access && istype(worn_id))
		worn_id.access |= dep_access

	var/teleport = 0
	if(!CONFIG_GET(flag/sec_start_brig))
		if(destination || spawn_point)
			teleport = 1
	if(teleport)
		var/turf/T
		if(spawn_point)
			T = get_turf(spawn_point)
			H.Move(T)
		else
			var/safety = 0
			while(safety < 25)
				T = safepick(get_area_turfs(destination))
				if(T && !H.Move(T))
					safety += 1
					continue
				else
					break
	if(department)
		to_chat(H, "<b>You have been assigned to [department]!</b>")
	else
		to_chat(H, "<b>You have not been assigned to any department. Patrol the halls and help where needed.</b>")

/datum/outfit/job/security
	name = "Security Officer"
	jobtype = /datum/job/officer

	glasses = /obj/item/clothing/glasses/hud/security/sunglasses
	mask = /obj/item/clothing/mask/gas/sechailer
	belt = /obj/item/storage/belt/security
	ears = /obj/item/radio/headset/headset_sec/alt
	uniform = /obj/item/clothing/under/rank/security/officer
	gloves = /obj/item/clothing/gloves/color/black
	head = /obj/item/clothing/head/helmet/sec
	suit = /obj/item/clothing/suit/armor/vest/alt
	shoes = /obj/item/clothing/shoes/jackboots/sec
	l_pocket = /obj/item/storage/bag/security
	r_pocket = /obj/item/modular_computer/pda/security
	backpack_contents = list(/obj/item/storage/ifak, /obj/item/storage/box/sec_kit,
						/obj/item/choice_beacon/copgun
						)
						
	backpack = /obj/item/storage/backpack/security
	satchel = /obj/item/storage/backpack/satchel/sec
	duffelbag = /obj/item/storage/backpack/duffelbag/sec
	box = /obj/item/storage/box/survival/security

	implants = list(/obj/item/implant/mindshield)
	accessory = list(/obj/item/clothing/accessory/permit/special/security, /obj/item/clothing/accessory/badge)

	chameleon_extras = list(/obj/item/gun/energy/disabler, /obj/item/clothing/glasses/hud/security/sunglasses, /obj/item/clothing/head/helmet)

/datum/outfit/job/security/syndicate
	name = "Syndicate Security Officer"
	jobtype = /datum/job/officer

	ears = /obj/item/radio/headset/headset_sec/alt
	uniform = /obj/item/clothing/under/rank/security/officer/util
	gloves = /obj/item/clothing/gloves/combat
	head = /obj/item/clothing/head/helmet/sec
	suit = /obj/item/clothing/suit/armor/vest/alt
	shoes = /obj/item/clothing/shoes/jackboots/tall_default
	backpack_contents = list(/obj/item/storage/ifak, /obj/item/storage/box/sec_kit,
						/obj/item/gun/ballistic/automatic/pistol/enforcer/nomag,
						/obj/item/ammo_box/magazine/e45/taser=3, /obj/item/syndicate_uplink/station=1)

	no_custom_backpack = TRUE
	backpack = /obj/item/storage/backpack/duffelbag/syndie/ammo
	satchel = /obj/item/storage/backpack/duffelbag/syndie/ammo
	duffelbag = /obj/item/storage/backpack/duffelbag/syndie/ammo
	box = /obj/item/storage/box/survival/syndie
	accessory = list(/obj/item/clothing/accessory/permit/special/security, /obj/item/clothing/accessory/permit/special/syndie_station)
	pda_slot = ITEM_SLOT_RPOCKET

/obj/item/radio/headset/headset_sec/alt/department/Initialize(mapload)
	. = ..()
	wires = new/datum/wires/radio(src)
	secure_radio_connections = new
	recalculateChannels()

/obj/item/radio/headset/headset_sec/alt/department/engi
	keyslot = new /obj/item/encryptionkey/headset_sec
	keyslot2 = new /obj/item/encryptionkey/headset_eng

/obj/item/radio/headset/headset_sec/alt/department/supply
	keyslot = new /obj/item/encryptionkey/headset_sec
	keyslot2 = new /obj/item/encryptionkey/headset_cargo

/obj/item/radio/headset/headset_sec/alt/department/med
	keyslot = new /obj/item/encryptionkey/headset_sec
	keyslot2 = new /obj/item/encryptionkey/headset_med

/obj/item/radio/headset/headset_sec/alt/department/sci
	keyslot = new /obj/item/encryptionkey/headset_sec
	keyslot2 = new /obj/item/encryptionkey/headset_sci
